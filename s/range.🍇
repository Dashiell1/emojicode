📗
  Range, an immutable sequence of numbers. [Read more in the
  language reference.](../../reference/the-s-package.html#-ranges)
📗
🌍 🕊 ⏩ 🍇
  🐊 🔂🐚🔢🍆
  🐊 🐽️🐚🔢🍆

  🖍🆕 start 🔢
  🖍🆕 stop 🔢
  🖍🆕 step 🔢

  📗
    Creates a range from *start* to *stop*.
  📗
  🆕 ⏩ 🍼 start 🔢 🍼 stop 🔢 🍇
    1 ➡️ 🖍step
    ↪️ stop ◀️ start 🍇
      -1 ➡️ 🖍step
    🍉
  🍉

  📗
    Creates a range from *start* to *stop* with the provided *step*.
  📗
  🆕 ⏭ 🍼 start 🔢 🍼 stop 🔢 🍼 step 🔢 🍇🍉

  📗
    Returns the *n*th element of the range. *n* is only valid if
    `start + step * n` is `>= start` and `< stop`. If *n* is invalid, the
    program will panic.
  📗
  ❗️ 🐽 n 🔢 ➡️ 🔢 🍇
    start ➕ n ✖️ step ➡️ v
    ↪️ step ◀️ 0 🍇
      ↪️ stop ◀️ v 🤝 v ◀️🙌 start 🍇
        ↩️ v
      🍉
    🍉
    🙅 🍇
      ↪️ start ◀️🙌 v 🤝 v ◀️ stop 🍇
        ↩️ v
      🍉
    🍉
    🤯🐇💻 🔤Index out of bounds in ⏩🐽❗️🔤 ❗️
    ↩️ 0
  🍉

  📗 Returns the number of integers in this range. 📗
  ❗️ 🐔 ➡️ 🔢 🍇
    ↩️ 🤜stop ➖ start🤛 ➗ step
  🍉

  📗 Returns an iterator to iterate over integers in this range. 📗
  ❗️ 🍡 ➡️ 🌳🐚🔢🍆 🍇
    ↩️ 🆕🌳🐚🔢🍆🆕🐕❗️
  🍉
🍉
